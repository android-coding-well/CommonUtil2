apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.greenrobot.greendao'
android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    dataBinding {
        enabled = true
    }
    //生成指定的so目录，避免出现找不到class的异常
    splits {
        abi {
            enable true
            reset()
            include 'armeabi'
            universalApk false
        }
    }

    lintOptions {
        abortOnError false
    }

    //重命名生成的apk
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent, defaultConfig.applicationId + "-" + buildType.name + "-v" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk");
        }
    }

    //加入compileOptions,这会让IDE使用用JAVA8语法解析
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //指定将源码编译的级别,，使用下列代码，会将代码编译到兼容1.6的字节码格式
    retrolambda {
        javaVersion JavaVersion.VERSION_1_6
    }

    defaultConfig {
        applicationId "com.gosuncn.cu"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        multiDexEnabled true // dex突破65535的限制
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
    //解决meta-inf冲突
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

greendao {
    schemaVersion 1//指定数据库schema版本号，迁移等操作会用到
    daoPackage 'com.gosuncn.cu.db.dao'//通过gradle插件生成的数据库相关文件的包名，默认为你的entity所在的包名
    targetGenDir 'src/main/java'//自定义生成数据库文件的目录,如果没指定则默认在build文件夹里
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.android.support:design:24.0.0'
    // compile 'com.android.support:support-v4:24.0.0'
    compile project(':lib_common')
    //compile 'com.github.huweijian5:CommonUtil2:2.0.3'
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    // dagger2
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    //网络请求库
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    //数据库
    compile 'org.greenrobot:greendao:3.0.1'
    //链式编程
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    //图片缩放
    compile 'com.commit451:PhotoView:1.2.4'
    //事件总线
    compile 'org.greenrobot:eventbus:3.0.0'
    //圆形图片 https://github.com/hdodenhof/CircleImageView
    compile 'de.hdodenhof:circleimageview:2.1.0'
    //拍照，图片单选多选裁剪旋转，详情戳这里：https://github.com/pengjianbo/GalleryFinal
    compile 'cn.finalteam:galleryfinal:1.4.8.7'
    //允许时权限管理https://github.com/tbruyelle/RxPermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'

    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha8'
}
